plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.1.5'
}
defaultTasks 'clean', 'licenseFormat'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'org.neptunepowered.gravel'
archivesBaseName = project.name.toLowerCase()
description = "A small Minecraft plugin API, for server-side plugins."
version = "1.0.0-SNAPSHOT"
ext.url = 'https://www.neptunepowered.org/'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:19.0'
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

license {
    header file('HEADER.txt')
    include '**/*.java'
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (System.getenv('MAVEN_RELEASES') != null) {
                repository(url: 'file://' + System.getenv('MAVEN_RELEASES'))
                snapshotRepository(url: 'file://' + System.getenv('MAVEN_SNAPSHOTS'))
            }

            pom {
                artifactId = project.archivesBaseName.toLowerCase()

                project {
                    name = project.name
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = "https://github.com//GravelPowered/GravelAPI"
                        connection = "scm:git:https://github.com//GravelPowered/GravelAPI.git"
                        developerConnection = "scm:git:git@github.com:/GravelPowered/GravelAPI.git"
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/GravelPowered/GravelAPI/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
